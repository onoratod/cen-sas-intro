{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Macro Variables \n",
    "\n",
    "Generally, the final component of a program header are the program-specific macro variables. SAS macro variables allow you to modify the text in a SAS program through symbolic substitution (think `local` and `global` from Stata). SAS macro variables are declared by a `%LET` statement followed by the macro name, an equals sign, and finally the value to be stored in the macro. You can store anything in a SAS macro. \n",
    "\n",
    "```sas\n",
    "%let fcohort = 1978;\n",
    "%let lcohort = 1991;\n",
    "%let file_name = example.txt;\n",
    "```\n",
    "\n",
    "To refer to a previously declared SAS macro variable, you type the macro name (i.e. fcohort) contained by `&` and `.`. For example, to keep rows of data for kids born between 1978-1991 using the macro variables above, we would write:\n",
    "\n",
    "```sas\n",
    "if cohort >= &fcohort. and cohort <= &lcohort.;\n",
    "```\n",
    "\n",
    "## A note on the use of `&` and `.` with Macros and Nested Macros\n",
    "\n",
    "Here we discuss macro processing in more detail, you should skip this portion for now and come back when useful. In general, there is no need to include the `.` delimiter at the end of the macro name when envoking a SAS macro variable - the `&` will suffice. For example, the sample code above will work without the periods: \n",
    "\n",
    "```sas\n",
    "if cohort >= &fcohort and cohort <= &lcohort;\n",
    "```\n",
    "\n",
    "There are times, however, when the `.` is required to delimit the end of a macro variable, so it is good practice to include them. For example, when using macro variables nested within text or code, the `.` is needed to signify the end of the macro variable. This is illustrated below:"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "tags": [
     "popout"
    ]
   },
   "source": [
    "````{margin}\n",
    "```{tip}\n",
    "The `%PUT` command acts like a `print` (Python) or `display` (Stata) statement, displaying the following text in the SAS log. This can be a useful tool for debugging.\n",
    "```\n",
    "````"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "First we define our macro `suffix`\n",
    "\n",
    "```sas\n",
    "%let suffix = ing;\n",
    "```\n",
    "\n",
    "The safe way to invoke this macro is \n",
    "\n",
    "```sas\n",
    "* This is the safe way;\n",
    "%PUT swimm&suffix.;\n",
    "```\n",
    "\n",
    "which would output\n",
    "\n",
    "```\n",
    "57   * This is the safe way;\n",
    "58   %PUT swimm&suffix.;\n",
    "swimming\n",
    "```\n",
    "\n",
    "However, in this case we can omit the `.` and get the same result\n",
    "\n",
    "```sas\n",
    "* This gives the same result;\n",
    "%PUT swimm&suffix;\n",
    "```\n",
    "\n",
    "which gives \n",
    "\n",
    "```\n",
    "60   * This gives the same result;\n",
    "61   %PUT swimm&suffix;\n",
    "swimming\n",
    "```\n",
    "\n",
    "If there were characters following the macro that are not part of the macro then omitting the `.` character would cause an error as below where SAS looks for the macro named `suffixly`\n",
    "\n",
    "```sas\n",
    "* This will not work, SAS looks for macro \"suffixly\";\n",
    "%PUT swimm&suffixly;\n",
    "```\n",
    "```\n",
    "63   * This will not work, SAS looks for macro \"suffixly\";\n",
    "WARNING: Apparent symbolic reference SUFFIXLY not resolved.\n",
    "64   %PUT swimm&suffixly;\n",
    "swimm&suffixly\n",
    "```\n",
    "\n",
    "Adding the `.` to denote the end of the macro fixes this \n",
    "\n",
    "```sas\n",
    "* This works, SAS knows macro is suffix;\n",
    "%PUT swimm&suffix.ly;\n",
    "```\n",
    "\n",
    "```\n",
    "66   * This works, SAS knows macro is suffix;\n",
    "67   %PUT swimm&suffix.ly;\n",
    "swimmingly\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "You can see how SAS interprets the different macro variables we defined in the code above. The `%PUT` statement acts like a print statement here and the output can be seen under lines 61, 64, and 67.\n",
    "\n",
    "### Nested Macros\n",
    "\n",
    "We can also nest macro variables inside of each other, although things become a bit more complicated here. If you want to understand how SAS will resolve these macros you need to understand the rules of SAS macro variables\n",
    "\n",
    "1. Never talk about SAS macro variables\n",
    "2. Two ampersands always resolve to one ampersand. \n",
    "3. The macro parser always continues reading left to right until the end of the reference is reached.\n",
    "4. If a reference contains consecutive ampersands, after resolution at least one more read will follow.\n",
    "\n",
    "To illustrate these rules lets consider a few examples. First lets define some macro variables\n",
    "\n",
    "```sas\n",
    "%let year = 99;\n",
    "%let century = 19;\n",
    "%let cpi1999 = 1.05;\n",
    "```\n",
    "\n",
    "Think about how the macro parser will treat\n",
    "\n",
    "```sas\n",
    "%PUT &cpi&century&year;\n",
    "```\n",
    "\n",
    "First it will try to resolve `&cpi`, however this doesn't exist so it will leave it as is. Then it will resolve `&century` to `19` and `&year` to `99` which results in `&cpi1999` since there were no consecutive ampersands the macro processor terminates. \n",
    "\n",
    "```\n",
    "WARNING: Apparent symbolic reference CPI not resolved.\n",
    "74   %PUT &cpi&century&year;\n",
    "&cpi1999\n",
    "```\n",
    "\n",
    "What if we use two ampersands in the beginning \n",
    "\n",
    "```sas\n",
    "%PUT &&cpi&century&year;\n",
    "```\n",
    "\n",
    "First the macro parser resolves `&&` to `&` then it tries to resolve `&century` followed by `&year` resulting in `&cpi1999`. Since we had consecutive ampersands the macro parser does one more read and resolves `&cpi1999` to `1.05`\n",
    "\n",
    "```\n",
    "90   %PUT &&cpi&century&year;\n",
    "1.05\n",
    "```\n",
    "\n",
    "Now let's try something a little more complicated\n",
    "\n",
    "\n",
    "```sas\n",
    "%let pre = cpi\n",
    "%let suf = year\n",
    "%let cpiyear = 1999\n",
    "%let cpi1999 = 1.07\n",
    "```\n",
    "\n",
    "Let's try to invoke `cpi1999` using `pre`, `suf`, and `cpiyear`. Let's start from the final line we want\n",
    "\n",
    "```sas\n",
    "%PUT &cpi1999;\n",
    "```\n",
    "\n",
    "Now we want `&cpiyear` to resolve to `1999` and we want a singular ampersand in the front. So we sub `&cpiyear` for `1999` and `&&` for `&` (because two ampersands resolve to one)\n",
    "\n",
    "```sas\n",
    "%PUT &&cpi&cpiyear;\n",
    "```\n",
    "\n",
    "Now we need to sub in `&pre` for `cpi` and `&suf` for `year`\n",
    "\n",
    "```sas\n",
    "%PUT &&cpi&&pre&suf;\n",
    "```\n",
    "\n",
    "However, this will not work and will resolve to `&cpi&preyear`. This is because we forgot to deal with `&&` and `&`. To retain `&&` we should use `&&&&` and to retain `&` we should use `&&` which gives\n",
    "\n",
    "```sas\n",
    "%PUT &&&&cpi&&&pre&suf;\n",
    "```\n",
    "\n",
    "which gives\n",
    "\n",
    "```sas\n",
    "54 %PUT &&&&cpi&&&pre&suf;\n",
    "1.07\n",
    "55\n",
    "```\n",
    "\n",
    "Let's step through this. In the first pass, the macro parser first resolves `&&&&cpi` to `&&cpi`, then `&&` to `&`, then `&pre` to `cpi`, then `&suf` to `year` giving `&&cpi&cpiyear`. On the second pass, it resolves `&&cpi` to `&cpi` and `&cpiyear` to `1999` giving `&cpi1999`. Finally, since there was a double ampersand, the macro parser does one final pass and resolves `&cpi1999` to `1.07`. \n",
    "\n",
    "This example was needlessly complicated to illustrate some of the macro parser rules. You will rarely have to use something this complicated but it may come in handy at some point. \n",
    "\n",
    "### References\n",
    "\n",
    "Referenced [this](https://support.sas.com/resources/papers/proceedings/proceedings/sugi29/063-29.pdf) source for this page."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
